{ ----------- Insert variables here -------- }

142 constant row                                          { Number of rows+2 }
142 constant col                                          { Number of columns+2 }
col 2 - constant col_small                               { Define number of rows and columns }
row 2 - constant row_small
row_small col_small * constant size                      { Size of the life board }

variable conway-file-id
variable conway-file-id-2
variable sum                                              { Variable to count number of neighbours }
variable count_alive                                      { Variable to count live cells }
variable count_dead                                       { Variable to count dead cells }
variable edges                                            { Variable to count live cells on edges }



CREATE SEED  123475689 ,

: Rnd ( n -- rnd )                                         { Returns single random number less than n }
   SEED                                                    { Minimal version of SwiftForth Rnd.f      }
   DUP >R                                                  { Algorithm Rick VanNorman  rvn@forth.com  }
   @ 127773 /MOD 
   2836 * SWAP 16807 * 
   2DUP > IF - 
   ELSE - 2147483647 +  
   THEN  DUP R> !
   SWAP MOD ;

: make_array * allocate drop ; ( row col --> )             { Create a new array }

{ ------------------------------------------ Create the life board arrays------------------------------- }

row_small col_small make_array constant life_board          { Create life board array }
row_small col_small make_array constant life_board_inverted { Create inverted life board array }


{ ------------------------------------------- Array manipulation words ----------------------------------- }

: array_! ( addr n i j --> )                                { Add element n to position i, j }
  swap col * ( addr n i j --> addr n j i*col )
  + ( addr n j i*col --> addr n j+i*col )
  rot + ( addr n j+i*col --> n addr + j+i*col )   
  c! ; 

: array_@ ( addr i j --> cell_val )                         { Get element i j }
  swap col * ( addr i j --> addr j i * col )
  + + ( addr j i * col --> addr+j+i*col )
  c@  ;

: array_@_small ( addr i j --> cell_val )                   { Get element from a smaller board }
    swap col_small * ( addr i j --> addr j i * col )
    + + ( addr j i * col --> addr+j+i*col )
    c@  ;

: array_!_small ( addr n i j --> )                          { Add element n to position i, j for smaller board}
  swap col_small * ( addr n i j --> addr n j i*col )
  + ( addr n j i*col --> addr n j+i*col )
  rot + ( addr n j+i*col --> n addr + j+i*col )   
  c! ;


: show_array ( addr col row --> )                           { Show the larger array }
  row 0 do cr                                                  
  col 0 do dup ( addr --> addr addr )
  J I  array_@ .                                               
  loop loop 
  drop ;                                                       

: show_small_array ( addr --> )                              { show array without BC }
    row_small 0 do cr
        col_small 0 do dup
            J row_small * ( --> addr addr j*row_small ) 
            I + +
            c@ .
        loop
    loop drop ;


: fill_rnd ( addr--> )                                        { Fill array with random numbers }
  row 0 do
  col 0 do ( addr --> addr )
  dup 2 rnd J I ( addr --> addr addr rnd8 J I )
  array_! loop loop
  drop ; 

: fill_0 ( addr --> )                                         { Fill an array with zeros }
    row 0 do
        col 0 do
            dup 0 J I array_!
        loop
    loop drop ;




: count_neighbours ( addr i j --> sum )                       { Count the number of neighbours for cell i, j }
    swap col * ( addr i j --> addr j i*col )
    + + ( --> addr + j + i*col ) 
    >R 
    R@ 1 + c@ sum c+!
    R@ 1 - c@ sum c+!
    R@ col - c@ sum c+!
    R@ col + c@ sum c+!
    R@ col - 1 + c@ sum c+!
    R@ col + 1 + c@ sum c+!
    R@ col + 1 - c@ sum c+!
    R@ col - 1 - c@ sum c+!
    R> drop 
    sum c@ 0 sum c! ;                                         { replace sum with 0 for next iteration }

: life_rules_alive ( n--> 1/0 )                               { Life rules based on number of neigbouring cells output 1/0 }
  dup ( n --> n n )
  case
    0 of drop 0 endof
    1 of drop 0 endof
    2 of drop 1 endof
    3 of drop 1 endof
    4 of drop 0 endof
    5 of drop 0 endof
    6 of drop 0 endof
    7 of drop 0 endof
    8 of drop 0 endof
  endcase ; 

: life_rules_dead ( n--> 1/0 )                                { Life rule to be applied if the cell is initially dead }
    3 = if
        1
    else 
        0
    then ;

: life_rules ( n dead/alive --> 1/0 )                         { Apply life rules depending on initial state }
    true = if ( n dead/alive --> n)
        life_rules_alive ( n --> alive/dead)
    else    
        life_rules_dead ( n --> alive/dead)
    then ;

: check_alive ( n --> true/false )                             { Check if it is alive }
    1 = if
        true
    else
        false
    then ;

: scan_array ( output_addr input_addr --> )                     { Scan the input array and input into update array }
    row 1 - 1 do
        col 1 - 1 do
            2dup dup 
            J I count_neighbours 
            swap J I array_@ 
            check_alive
            life_rules 
            J I array_! 
        loop
    loop drop drop ;

: fill_row_0 ( addr i --> )                                      { Fill a row with 0s }
    col 0 do 
        2dup 
        0 swap I array_! 
    loop drop drop ;

: fill_edge_0 ( addr i --> )                                     { Fill the first and last column with 0 }
    2dup ( --> addr i addr i )
    0 swap ( --> addr i addr 0 i ) 0 array_!
    0 swap ( --> addr i addr 0 i ) col 1 - array_!
    ;

: no_wrap ( addr --> )                                            { Update the array with absorbing bc }
    row 0 do dup  ( --> addr addr )
        I dup ( --> addr addr I I )
        case
            0 of ( --> addr addr I ) fill_row_0 endof ( --> addr )
            row 1 - of fill_row_0 endof
        drop fill_edge_0 dup 
        endcase
    loop drop ;

: fill_row_wrap  ( addr i --> )                                    { Apply periodic BCs to a specified rows }
    col 1 - 1 do 
        2dup 2dup ( --> addr i addr i addr i )
        row 2 - swap - abs ( --> addr i addr i addr row-2-i )
        I array_@ ( --> addr i addr i dead/alive )
        swap ( --> addr i addr dead/alive i )
        I array_! ( --> addr i )
    loop 
    2dup 2dup 2dup ( --> addr i addr i addr i )
    row 2 - ( --> addr i addr i addr i row -2 ) - ( i - row + 2) abs 
    col 2 - ( --> addr i addr i addr i +2 - row col-2 ) array_@ swap 0 array_! 
    ( --> addr i addr i ) 
    row 2 - - ( --> addr i addr i+2-row ) abs 1 array_@ 
    ( --> addr i cell_val )
    swap ( --> addr cell_val i ) col 1 - array_!
    ;

: fill_edge_wrap ( addr i --> )                                     { Apply BCs to particular columns }
    2dup 2dup 
    col 2 - array_@ swap
    0 array_! 2dup
    1 array_@ swap 
    col 1 - array_!
    ;

: wrapping ( addr --> )                                             { Apply the periodic BCs to the entire array }
    row 0 do dup  ( --> addr addr )
        I dup ( --> addr addr I I )
        case
            0 of ( --> addr addr I ) fill_row_wrap endof ( --> addr )
            row 1 - of fill_row_wrap endof
        drop fill_edge_wrap dup 
        endcase
    loop drop ;

: set_matrix_equal ( addr 1 addr 2 --> )                             { Fill matrix with elements of other matrix }
    row 0 do
        col 0 do 
            2dup J I array_@ 
            J I array_!
        loop
    loop drop drop ;

: remove_BC ( addr --> )                                             { Remove the BCs }
    row 1 - 1 do
        col 1 - 1 do
            dup ( --> addr addr )
            J I array_@ ( --> addr n )
            I 1 - ( --> addr n i-1)
            J 1 - ( --> addr n i-1 j-1)
            col_small * + ( --> addr n j' + i'*col-2 )
            life_board + c!
        loop
    loop drop ;

: check_edges_alive ( --> true/false )                                { Check to see if the edges are alive }
    col_small 0 do  
        life_board 0 I array_@_small edges +!
        life_board col_small 1 - I array_@_small edges +!
    loop
    row_small 1 - 1 do
        life_board I 0 array_@_small edges +!
        life_board I col_small 1 - array_@_small edges +!
    loop
    edges @ 0 = if
        false
    else
        true
    then ;

        
{ ------------------------------------------- Writing data to files -------------------------------------------------- }



: make-file
    s" C:\Users\Joe\Conway_Files.txt " r/w create-file drop
    conway-file-id !
    ;

: make-activity-file
    s" C:\Users\Joe\Documents\Conway_Files_Activity.txt " r/w create-file drop
    conway-file-id-2 !
    ;



: write-matrix    ( addr --> )                                       { Write the life board to the conway-file }
    row_small 0 do 
        col_small 0 do   
            dup ( --> addr addr )
            J I array_@_small (.) conway-file-id @ write-file drop
            s"  " conway-file-id @ write-file drop
        loop 
    loop drop ;

: write-blank                                                       { Write an empty line to the file       }
  s"  " conway-file-id @ write-line drop
;


: close-file                                                        { Close the file pointed to by the file handle }
  conway-file-id @                                                                  
  close-file drop
  conway-file-id-2 @
  close-file drop
; 


{ ------------------------------------------------- Creating GIO ---------------------------------------------- }

INCLUDING-OPTION C:\Users\Joe\Documents\Forth\Forth_Project` bmp-display-2.f 

: rearrange_board                                                           
    row_small 0 do
        col_small 0 do
            life_board J I array_@_small 
            life_board_inverted swap row_small 1 - J - I array_!_small
        loop
    loop ;

: go_visual
  rearrange_board
  bmp-address @ write_bmp
  bmp-address @ bmp-to-screen-stretch
  100 ms
  ;






{ ---------------------------------------------------------- Calculate Statistics --------------------------------------- }

: count_statistics ( addr --> count_alive count_dead )                    { Count live and dead cells }
    0 count_alive !
    0 count_dead !
    row 1 - 1 do
        col 1 - 1 do
            dup 
            J I array_@ ( --> addr 1/0 ) dup 
            case
                1 of 1 count_alive +! drop endof
                0 of 1 count_dead +! drop endof
            endcase 
        loop
    loop drop
    count_alive @ count_dead @
    ;

: change_statistics ( addr1 addr 2 --> change_dead change_alive )         { Count change in live and change in dead cells }
    count_statistics ( --> addr1 count_alive count_dead )
    rot count_statistics ( --> count_alive_1 count_dead_1 cout_alive_2 count_dead_2 )
    rot ( --> count_alive_1 count_alive_2 count_dead_2 count_dead_1 )
    swap - ( --> count_alive_1 count_alive_2 count_dead_1 - count_dead_2 )
    -rot - 
;





{ -------------------------------------------------------- Running Conway ---------------------------------------------------- }

1500 constant iterations                                                  { Number of iterations }
0 constant wrap                                                          { 0 for non wrapping and 1 for wrapping boundaries }
0 constant boundary_checking                                             { 1 to check is cells are alive in boundaries }
0 constant visualisation                                                 { 1 for visualisation 0 for none }
0 constant verbose                                                       { 1 to show statistics 0 for none }

row col make_array constant matrix
row col make_array constant new_matrix

matrix fill_0
new_matrix fill_0




{ -------------------------- Initialise matrix with seed --------------------- }

: horizontal_row ( n--> )                                                 { Input a horizontal row at the center of the board }
    dup 2 / col 2 / ( --> n/2 col/2 )
    swap 2dup - ( --> col/2 n/2 col/2-n/2 )
    -rot + rot
    2 mod 0= if
    else
        1 +
    then 
    swap ( --> col/2-n/2 col/2+n/2 ) 
    do
        1 row 2 / ( --> 1 row/2 ) 
        matrix -rot I array_!
    loop ;

: fill_50                                                                   { Fill with 50-50 random }
    Begin 
        matrix fill_rnd
        matrix count_statistics
        =
     until ;

: fill_percentage ( perc --> )                                              { Fill a percentage of the initial matrix with random live cells }
    fill_50
    dup 50 - 
    0 > if
        Begin
            matrix 1
            row rnd
            col rnd
            array_!
            dup size * 100 /
            matrix count_statistics
            drop = 
        until
    else
        Begin
            matrix 0
            row rnd
            col rnd
            array_!
            dup size * 100 /
            matrix count_statistics
            drop = 
        until
        then
        drop ;

: reset                                                                         { Fill matrices with 0 }
    matrix fill_0
    new_matrix fill_0
;

60 fill_percentage



{ -------------------------- Run word ------------------------------- }

: run_conway
    time&date drop drop drop drop { Assuming only s and min }
    60 * + >r
    wrap 0= if
        matrix wrapping
        ." Running with Wrapping "
    else
        matrix no_wrap
        ." Running with no wrapping "
    then 
    make-file
    make-activity-file
    visualisation 1 = if   
        New-bmp-Window-stretch              
        bmp-window-handle !
    then
    iterations 0 do 
        verbose 1 = if
            cr ." Iteration " I . ." Running " cr 
            matrix count_statistics
            . ." Number of dead cells " cr 
            . ." Number of live cells " cr 
        then
        matrix remove_BC 
        life_board write-matrix 
        write-blank
        visualisation 1 = if    
            go_visual
        then
        new_matrix matrix scan_array
        wrap 0= if 
            new_matrix wrapping 
        else
            matrix no_wrap
        then 
        new_matrix matrix change_statistics dup 
        (.) conway-file-id-2 @ write-line drop 
        verbose 1 = if
            . ." Change in dead cells " cr 
            . ." Change in live cells " cr 
        else
            drop drop
        then 
        boundary_checking 1 = if
            check_edges_alive
            true = if
                ." Cell alive at edges " cr
            then
        then 
        matrix new_matrix set_matrix_equal
    loop
    time&date drop drop drop drop
    60 * + r> -
    ." Time taken " . ." s "
    close-file
    bmp-window-handle @ DestroyWindow  drop 
    ;



INCLUDING-OPTION C:\Users\Joe\Documents\Forth\Forth_Project` Temperature-Script.f 
















